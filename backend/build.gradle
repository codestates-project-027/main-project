plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id 'java'
	id 'jacoco'
}

group = 'com.minimi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

configurations {
	asciidoctorExtensions
}

dependencies {
	//restdocs mockmvc
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	//restdocs asiidoctor
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	//data jpa
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	//validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	//h2 database
	runtimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//mapstruct
	implementation 'org.mapstruct:mapstruct:1.5.1.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
	//gson
	implementation 'com.google.code.gson:gson'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	configurations "asciidoctorExtensions"
	inputs.dir snippetsDir
	dependsOn test
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	println "asciidoctor output: ${asciidoctor.outputDir}"
	from file("${asciidoctor.outputDir}")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	dependsOn copyDocument
	from ("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}
//jacoco
jacoco {
	toolVersion = "0.8.5"
}
test {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}
jacocoTestReport {
	reports {
		html.required.set(true)
		xml.required.set(false)
		csv.required.set(false)
//		html.destination file("src/jacoco/jacoco.html")
		afterEvaluate {
			classDirectories.setFrom(files(classDirectories.files.collect {
				fileTree(dir: it, exclude: [
						"com/minimi/backend/auth/**",
						"com/minimi/backend/community/**"
				])
			}))
		}
	}
	finalizedBy 'jacocoTestCoverageVerification'
}
jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"com/minimi/backend/auth/**",
					"com/minimi/backend/community/**"
			])
		}))
	}
	violationRules {
		rule {
			excludes = [
					"**.*Application*",
					"**.*Config*",
					"**.*Dto*",
					"**.*Request*",
					"**.*Response*",
					"**.*Interceptor*",
					"**.*Exception*",
					"**.*Mapper*"
			]
			element = 'CLASS'
			enabled = true

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.00
			}

			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.00
			}
		}
	}
}
